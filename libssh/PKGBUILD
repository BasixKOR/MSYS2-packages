pkgbase=libssh
pkgname=('libssh' 'libssh-devel')
pkgver=0.11.0
pkgrel=1
pkgdesc='Library for accessing ssh client services through C libraries'
url='https://www.libssh.org/'
msys2_repository_url="https://git.libssh.org/projects/libssh.git"
msys2_references=(
  "cpe: cpe:/a:libssh:libssh"
)
license=('spdx:LGPL-2.1+')
arch=('i686' 'x86_64')
depends=('zlib' 'libopenssl')
makedepends=('cmake' 'python' 'openssl-devel' 'gcc' 'zlib-devel')
source=("https://www.libssh.org/files/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.asc}
        "https://gitlab.com/libssh/libssh-mirror/-/merge_requests/526.patch")
sha256sums=('860e814579e7606f3fc3db98c5807bef2ab60f793ec871d81bcd23acdcdd3e91'
            'SKIP'
            '0933e3e3367adaafea01858eafd8aa240fcaf5c2c2b6a4fdc58ab3c06ce980f1')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D'  # Andreas Schneider <asn@cryptomilk.org>
              '88A228D89B07C2C77D0C780903D5DF8CFDD3E8E7') # libssh release key (release key) <libssh@libssh.org>

prepare() {
  cd $pkgname-$pkgver

  # https://gitlab.com/libssh/libssh-mirror/-/merge_requests/526
  patch -Np1 -i ../526.patch
}

build() {
  mkdir -p build && cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_GSSAPI=OFF \
    -DUNIT_TESTING=OFF
  make
  make DESTDIR="$srcdir/dest" install
}

package_libssh() {
  mkdir -p $pkgdir/usr/bin
  cp -rf $srcdir/dest/usr/bin/* $pkgdir/usr/bin/
}

package_libssh-devel() {
  mkdir -p $pkgdir/usr/include
  cp -rf $srcdir/dest/usr/include/* $pkgdir/usr/include/

  mkdir -p $pkgdir/usr/lib
  cp -rf $srcdir/dest/usr/lib/* $pkgdir/usr/lib/
}
