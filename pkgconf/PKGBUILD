# Maintainer: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=pkgconf
pkgver=2.5.0
pkgrel=2
pkgdesc='pkg-config compatible utility which does not depend on glib'
url='https://github.com/pkgconf/pkgconf'
msys2_references=(
  "cpe: cpe:/a:pkgconf:pkgconf"
)
arch=('i686' 'x86_64')
license=('spdx:ISC')
makedepends=('meson'
             'ninja'
             'gcc')
conflicts=('pkg-config')
provides=('pkg-config')
replaces=('pkg-config')
source=(https://github.com/pkgconf/pkgconf/archive/pkgconf-$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/pkgconf/pkgconf/commit/205bc7bf4266945f02d7313ccdd917074a91b09e.patch)
sha256sums=('89060c3b3e5e5c7d7101eefd6fb8df8d9bcdc6c14217390b925557b6c0064671'
            '06748b48bb31536215ed70bc2c4d3d6e040ebacdb51c3bc7e233c75f15969301')

prepare() {
  mv "${pkgname}-${pkgname}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  # https://github.com/pkgconf/pkgconf/issues/413
  patch -R -p1 < "${srcdir}"/205bc7bf4266945f02d7313ccdd917074a91b09e.patch
}

build() {
  mkdir "${srcdir}"/build && cd "${srcdir}"/build

  meson \
    --buildtype=plain \
     --prefix=/usr \
     -Dtests=disabled \
    "../${pkgname}-${pkgver}"

  meson compile
}

package() {
  cd "${srcdir}"/build

  DESTDIR="$pkgdir" meson install

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  cp "$pkgdir"/usr/bin/pkgconf "$pkgdir/usr/bin/${CYGWIN_CHOST}-pkg-config"
  cp "$pkgdir"/usr/bin/pkgconf "$pkgdir"/usr/bin/pkg-config
}
