# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=mako
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=1.3.10
pkgrel=1
pkgdesc="A super-fast templating language that borrows the best ideas from the existing templating languages"
arch=('any')
url="https://pypi.python.org/pypi/Mako"
msys2_repository_url="https://github.com/sqlalchemy/mako"
msys2_references=(
  "purl: pkg:pypi/mako"
  "cpe: cpe:/a:sqlalchemy:mako"
)
license=('MIT')
depends=("python-markupsafe"
         "python-beaker")
makedepends=(
  "python-build"
  "python-installer"
  "python-setuptools"
)
checkdepends=("python-pytest")
options=('staticlibs')
source=("https://pypi.python.org/packages/source/m/mako/mako-${pkgver}.tar.gz")
sha256sums=('99579a6f39583fa7e5630a28c3c1f440e4e97a414b80372649c0ce338da2ea28')

build() {
  cd "${srcdir}/Mako-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/Mako-${pkgver}"

  python -m pytest
}

package() {
  cd "${srcdir}/Mako-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"
}
