# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Try-Tiny
pkgname=perl-${_realname}
pkgver=0.32
pkgrel=1
pkgdesc="Minimal try/catch with proper localization of \$@"
arch=('any')
url="https://metacpan.org/dist/Try-Tiny"
msys2_references=(
  'purl: pkg:cpan/ETHER/Try-Tiny'
)
groups=('perl-modules')
license=('MIT')
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/ET/ETHER/${_realname}-${pkgver}.tar.gz")
sha256sums=('ef2d6cab0bad18e3ab1c4e6125cc5f695c7e459899f512451c8fa3ef83fa7fc0')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
