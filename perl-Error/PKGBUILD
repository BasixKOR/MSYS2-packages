# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Error
pkgname=perl-Error
pkgver=0.17030
pkgrel=1
pkgdesc="Perl/CPAN Error module - Error/exception handling in an OO-ish way"
url="https://metacpan.org/release/Error"
msys2_references=(
  "purl: pkg:cpan/SHLOMIF/Error"
)
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-Test-Pod' 'perl-test-pod-coverage')
options=('!emptydirs')
groups=('perl-modules')
source=(https://www.cpan.org/authors/id/S/SH/SHLOMIF/${_realname}-${pkgver}.tar.gz)
sha256sums=('34d382276c0fb0d6b38355b94c96a30b12d834d5662eb53f088ee25e3e712924')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

