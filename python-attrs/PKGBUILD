# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=attrs
pkgbase=python-attrs
pkgname=("python-${_realname}")
pkgver=25.3.0
pkgrel=1
pkgdesc="Attributes without boilerplate."
arch=('any')
license=('spdx:MIT')
url='https://www.attrs.org/'
msys2_repository_url='https://github.com/python-attrs/attrs'
msys2_references=(
  "purl: pkg:pypi/attrs"
)
depends=('python')
makedepends=(
  'python-hatchling'
  'python-hatch-vcs'
  'python-hatch-fancy-pypi-readme'
  'python-build'
  'python-installer'
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha512sums=('acf34c38865157c49ba4070b35a162f0eb976ded0a8b8f74afe4c7e7238b8418af6108954cfc92c9721a090505e73ae193225a73a02352ab3bba0d9ace189059')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
