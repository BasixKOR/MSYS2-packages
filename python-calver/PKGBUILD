# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=calver
pkgname=python-calver
pkgver=2025.4.2
pkgrel=1
pkgdesc="Setuptools extension for CalVer package versions"
arch=('any')
url="https://github.com/di/calver"
msys2_references=(
  "purl: pkg:pypi/calver"
)
license=('spdx:Apache-2.0')
depends=(
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('f854bb865a070da3d45f989f812b2fb6847a10c2d5a7490490ec16832617a463')

build() {
  cd "${_pyname}-${pkgver}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
