# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=mallard-ducktype
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=1.0.2
pkgrel=6
pkgdesc="Parser for the lightweight Ducktype syntax for Mallard"
arch=('any')
url="http://projectmallard.org/"
msys2_repository_url="https://github.com/projectmallard/mallard-ducktype"
msys2_references=(
  "pypi:mallard-ducktype"
  'archlinux: mallard-ducktype'
)
license=('spdx:MIT')
depends=("python")
makedepends=(
  "python-setuptools"
  "python-build"
  "python-installer"
)
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/projectmallard/mallard-ducktype/archive/${pkgver}.tar.gz")
sha256sums=('73ac416376dd65c737594ab4b1037b0823390239f33672f1ee1e30725dcef4e9')

build() {
  cd "${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_realname}-${pkgver}"

  (cd ./tests && ./runtests)
}

package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"
}
