# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_pyname=setuptools_scm
_realname=setuptools-scm
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=8.1.0
pkgrel=3
pkgdesc="Handles managing your python package versions in scm metadata"
arch=('any')
license=('spdx:MIT')
url='https://github.com/pypa/setuptools_scm'
msys2_references=(
  "anitya: 65053"
  "pypi:setuptools-scm"
)
depends=(
  'python'
  'python-setuptools'
  'python-packaging'
  'python-typing_extensions'
)
makedepends=(
  "python-build"
  "python-installer"
)
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('42dea1b65771cba93b7a515d65a65d8246e560768a66b9106a592c8e7f26c8a7')

build() {
  cd "${_pyname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
