# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Mozilla-CA
pkgname=perl-${_realname}
pkgver=20240730
pkgrel=1
pkgdesc="Mozilla's CA cert bundle in PEM format"
arch=('any')
license=('GPL2' 'LGPL2.1')
options=(!emptydirs)
depends=('perl>=5.006')
url="https://metacpan.org/dist/Mozilla-CA"
groups=('perl-modules')
source=("https://cpan.metacpan.org/authors/id/L/LW/LWP/Mozilla-CA-${pkgver}.tar.gz")
sha256sums=('55bbee1875a905616e0c39457245f94f8173227fbe906f643673f5adadedf863')

build() {
  cd ${_realname}-${pkgver}
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd ${_realname}-${pkgver}
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
