# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=diffstat
pkgname=diffstat
pkgver=1.66
pkgrel=1
pkgdesc="Display a histogram of diff changes"
arch=('i686' 'x86_64')
url="https://invisible-island.net/diffstat"
makedepends=('autotools' 'gcc')
license=('spdx:MIT')
source=("https://invisible-mirror.net/archives/diffstat/diffstat-${pkgver}.tgz"{,.asc})
sha256sums=('f54531bbe32e8e0fa461f018b41e3af516b632080172f361f05e50367ecbb69e'
            'SKIP')
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # Thomas Dickey <dickey@invisible-island.net>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
}

build () {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export MSYSTEM=CYGWIN
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure --build=${CYGWIN_CHOST} \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir}/ install
  install -Dm644 package/debian/copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
