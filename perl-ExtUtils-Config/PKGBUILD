# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ExtUtils-Config
pkgname=perl-${_realname}
pkgver=0.009
pkgrel=1
pkgdesc="ExtUtils::Config - A wrapper for perl's configuration"
arch=('any')
url="https://metacpan.org/dist/ExtUtils-Config"
groups=('perl-modules')
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/L/LE/LEONT/${_realname}-${pkgver}.tar.gz")
sha256sums=('4ef84e73aad50a3be332885d2a3b12f3cab1b1e0bad24e88297a123b4f39f3ce')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_realname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
