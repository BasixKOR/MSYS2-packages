# Maintainer: Orgad Shaneh <orgads@gmail.com>

pkgbase=libpcap
pkgname=('libpcap' 'libpcap-devel')
pkgver=1.10.4
pkgrel=1
pkgdesc="A portable C/C++ library for network traffic capture"
arch=('i686' 'x86_64')
license=('spdx:BSD-3-Clause')
url="https://www.tcpdump.org/"
msys2_repository_url="https://github.com/the-tcpdump-group/libpcap"
msys2_references=(
  "cpe: cpe:/a:tcpdump:libpcap"
)
makedepends=('bison' 'cmake' 'flex' 'gcc' 'make' 'openssl-devel')
source=(https://www.tcpdump.org/release/${pkgname}-${pkgver}.tar.gz
        0003-Disable-man-symlinks.patch)
sha256sums=('ed19a0383fad72e3ad435fd239d7cd80d64916b87269550159d20e47160ebe5f'
            'cc3e4218937f5c75448ddcb3b09b1ad90c11ca2a01ade1defe900cba60893e8e')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  patch -p1 -i ${srcdir}/0003-Disable-man-symlinks.patch
}

build() {
  mkdir -p build-${pkgbase}-${pkgver}-${CHOST}
  cd build-${pkgbase}-${pkgver}-${CHOST}
  cmake \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    ../${pkgbase}-${pkgver}
  make DESTDIR=${srcdir}/dest install
}

package_libpcap() {
  pkgdesc="A portable C/C++ library for network traffic capture"
  groups=('libraries')
  depends=('gcc-libs')
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_libpcap-devel() {
  pkgdesc="Libpcap headers and libraries"
  groups=('development')
  depends=("libpcap=${pkgver}")
  options=('staticlibs')

  mkdir -p ${pkgdir}/usr/bin

  cp -f ${srcdir}/dest/usr/bin/pcap-config ${pkgdir}/usr/bin
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
