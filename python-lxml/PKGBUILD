# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=lxml
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=5.2.2
pkgrel=3
pkgdesc="Python binding for the libxml2 and libxslt libraries"
arch=('any')
url="https://lxml.de/"
msys2_repository_url="https://github.com/lxml/lxml"
msys2_references=(
  "pypi:lxml"
  "cpe: cpe:/a:lxml:lxml"
)
license=('spdx:BSD-3-Clause')
depends=("python" "libxslt" "libxml2")
makedepends=(
  "gcc"
  "cython"
  "libxslt-devel"
  "libxml2-devel"
  "pkgconf"
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=("https://github.com/lxml/lxml/archive/${_realname}-${pkgver}.tar.gz")
sha256sums=('1801c8576a4fca14d46561f2963cebe4dad2c90f6d98aaaa4f241eff757e7c6c')

prepare() {
  mv "lxml-lxml-${pkgver}" "lxml-${pkgver}"

}

build() {
  cd "${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSES.txt "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSES.txt"
}
