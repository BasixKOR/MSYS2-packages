# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=typing_extensions
pkgname=python-typing_extensions
pkgver=4.13.2
pkgrel=1
pkgdesc='Backported and Experimental Type Hints for Python 3.8+'
arch=(any)
url=https://github.com/python/typing_extensions
msys2_references=(
  'purl: pkg:pypi/typing-extensions'
)
license=('spdx:PSF-2.0')
depends=(python)
makedepends=(
  python-build
  python-flit-core
  python-installer
)
source=("https://pypi.python.org/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('e6c81219bd689f51865d9e372991c540bda33a0379d5573cddb9a3a23f7caaef')

build() {
  cd "${_pyname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_pyname}-${pkgver}"

  python -m unittest discover src
}

package() {
  cd "${_pyname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
}
