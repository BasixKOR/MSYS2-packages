# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pluggy
pkgbase=python-pluggy
pkgname=(python-${_realname})
pkgver=1.5.0
pkgrel=3
pkgdesc="Plugin and hook calling mechanisms for python"
arch=('any')
url="https://github.com/pytest-dev/pluggy"
msys2_references=(
  "purl: pkg:pypi/pluggy"
)
license=('spdx:MIT')
depends=('python')
makedepends=(
  'python-setuptools'
  'python-setuptools-scm'
  'python-build'
  'python-installer'
)
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/pytest-dev/pluggy/archive/${pkgver}.tar.gz")
sha512sums=('81369ec92c3c0fd94f1602ea964abbb058f19f7f7cd53fde1e07713908b6d63aba92b576d853d9115e73869cc43ab645e08b7e8462a6f38fed7c6f7d3951e9b4')

prepare() {
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m pytest
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
