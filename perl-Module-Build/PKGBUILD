# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=Module-Build
pkgname=perl-${_realname}
pkgver=0.4234
pkgrel=1
pkgdesc="Build, test, and install Perl modules"
arch=('any')
url="https://metacpan.org/release/Module-Build"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.8.0' 'perl-CPAN-Meta>=2.142060' 'perl-inc-latest')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/L/LE/LEONT/${_realname}-${pkgver}.tar.gz
        module-build-0.4224-msysize.patch)
sha512sums=('e2bbdd64f98b424ea7fd20e2ba301d34b77c9f2ecec28e3191fda63a056a12c0fb1cab1496419e9e9b8054db1a8d3fe92e1f033cdcdf021e1a086c8859905eca'
            'dc65b955258a9c877684b19092002514186822fcb3e170fae53aa22fa500d37b0222a1f2734fc86752d7f168eb99cc54ffcd512227bbcd5b91ec11dc74219f0f')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/module-build-0.4224-msysize.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd ${_realname}-${pkgver}
  perl Build test
}

package() {
  cd ${_realname}-${pkgver}
  perl Build install destdir="${pkgdir}"
}
