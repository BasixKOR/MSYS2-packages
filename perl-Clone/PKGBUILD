# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Clone
pkgname=perl-${_realname}
pkgver=0.47
pkgrel=1
pkgdesc='Recursive copy of nested objects.'
arch=('i686' 'x86_64')
url='https://github.com/garu/Clone'
groups=('perl-modules')
license=('GPL' 'PerlArtistic')
depends=('perl')
makedepends=('perl-devel' 'gcc' 'make')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/A/AT/ATOOMIC/${_realname}-${pkgver}.tar.gz")
sha256sums=('4c2c0cb9a483efbf970cb1a75b2ca75b0e18cb84bcb5c09624f86e26b09c211d')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd ${_realname}-${pkgver}
    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
