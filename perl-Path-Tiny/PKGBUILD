# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Path-Tiny
pkgname=perl-${_realname}
pkgver=0.146
pkgrel=1
pkgdesc="File path utility"
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://metacpan.org/dist/Path-Tiny"
groups=('perl-modules')
depends=('perl>=5.8.1')
options=(!emptydirs)
source=(https://cpan.metacpan.org/authors/id/D/DA/DAGOLDEN/${_realname}-${pkgver}.tar.gz)
sha256sums=('861ef09bca68254e9ab24337bb6ec9d58593a792e9d68a27ee6bec2150f06741')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
}

check() (
  cd "${srcdir}/${_realname}-${pkgver}"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)
package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
