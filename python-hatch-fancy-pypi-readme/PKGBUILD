# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=hatch-fancy-pypi-readme
pkgbase=python-hatch-fancy-pypi-readme
pkgname=("python-${_realname}")
pkgver=24.1.0
pkgrel=2
pkgdesc="Fancy PyPI READMEs with Hatch"
arch=('any')
license=('spdx:MIT')
url="https://github.com/hynek/hatch-fancy-pypi-readme"
msys2_references=(
  "purl: pkg:pypi/hatch-fancy-pypi-readme"
)
depends=('python'
         'python-hatchling')
makedepends=(
  'python-build'
  'python-installer'
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname//-/_}-${pkgver}.tar.gz")
sha256sums=('44dd239f1a779b9dcf8ebc9401a611fd7f7e3e14578dcf22c265dfaf7c1514b8')

build() {
  cd "${srcdir}/${_realname//-/_}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname//-/_}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
