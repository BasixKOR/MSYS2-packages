# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=IO-Socket-SSL
pkgname=perl-${_realname}
pkgver=2.089
pkgrel=1
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://metacpan.org/dist/IO-Socket-SSL"
groups=('perl-modules')
depends=('perl-Net-SSLeay' 'perl' 'perl-URI')
checkdepends=('perl-IO-Socket-INET6')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/S/SU/SULLR/${_realname}-${pkgver}.tar.gz")
sha256sums=('f683112c1642967e9149f51ad553eccd017833b2f22eb23a9055609d2e3a14d1')

build() {
  cd ${_realname}-${pkgver}
  yes | perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test || true
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
