# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=IO-Socket-SSL
pkgname=perl-${_realname}
pkgver=2.094
pkgrel=1
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://metacpan.org/dist/IO-Socket-SSL"
msys2_references=(
  'purl: pkg:cpan/SULLR/IO-Socket-SSL'
)
groups=('perl-modules')
depends=('perl-Net-SSLeay' 'perl' 'perl-URI')
checkdepends=('perl-IO-Socket-INET6')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/S/SU/SULLR/${_realname}-${pkgver}.tar.gz")
sha256sums=('b2446889cb5e20545d782c4676da1b235673a81c181689aaae2492589d84bf02')

build() {
  cd ${_realname}-${pkgver}
  yes | perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test || true
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
