# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=libtirpc
pkgname=('libtirpc' 'libtirpc-devel')
pkgver=1.3.4
pkgrel=1
pkgdesc="A port of Sun's Transport-Independent RPC library"
arch=('i686' 'x86_64')
license=('BSD')
url="https://git.linux-nfs.org/?p=steved/libtirpc.git;a=summary"
msys2_references=(
  "cpe: cpe:/a:libtirpc_project:libtirpc"
  "cpe: cpe:/a:redhat:libtirpc"
)
makedepends=('gcc' 'patch' 'heimdal-devel' 'autotools')
source=(https://downloads.sourceforge.net/libtirpc/libtirpc-${pkgver}.tar.bz2
        0.2.4-cygwin-ipv6.patch
        0.2.4-cygwin-poll.patch
        0.2.4-cygwin-pthread.patch
        0.2.4-cygwin-rpc-types.patch
        0.3.2-no-undefined.patch
        1.3.2-cygwin-rpcent.patch
        1.3.2-cygwin-types.patch
        1.3.3-cygwin-bindresvport.patch
        1.3.3-includedir.patch
        1.3.4-no-errqueue.patch)
sha256sums=('1e0b0c7231c5fa122e06c0609a76723664d068b0dba3b8219b63e6340b347860'
            '24d8f179812434e3be91800eb4387fec38d9050f3d1902ee1e66246c776fac11'
            '90cedf3f930bf44dbbed2e520305df1a53daddac464d1d68edf501466578ac0c'
            'd45ae803c401ca5a37e5084da822ff7f448241be38d457ef981692322e7fd057'
            'c247c94a431c0548610c6e97b620a3f81767a31f894b14027737b8afbade95f8'
            '19f6c539781476c5ac7fd58f080ca587c4c70c69dd82d57e30d69d39b9c7ac9f'
            '46039746d183cc7d8c26f87ed6529578720589d0257f4bb3305318073d0b7964'
            '6a93776a8a1a40c67455b35ebec9452d36da6752ab9defbc84e147e3edb33150'
            'e7e3eea865c48638d86a91d56a1b1626173f542b86367b82222a04b6b9c15db0'
            '46ee748de1794775f03f049f6e8cfb24f9f2760b0efae8a4258c13bc3ec80f6d'
            '56b25ddbfbee6eaa204151c77ce15d897ed4e9fb938e91048998127cb3deb373')

prepare() {
  cd libtirpc-${pkgver}

  # from cygwin: https://cygwin.com/cgit/cygwin-packages/libtirpc
  patch -p2 -i ${srcdir}/0.2.4-cygwin-ipv6.patch
  patch -p2 -i ${srcdir}/0.2.4-cygwin-poll.patch
  patch -p2 -i ${srcdir}/0.2.4-cygwin-pthread.patch
  patch -p2 -i ${srcdir}/0.2.4-cygwin-rpc-types.patch
  patch -p2 -i ${srcdir}/0.3.2-no-undefined.patch
  patch -p2 -i ${srcdir}/1.3.2-cygwin-rpcent.patch
  patch -p2 -i ${srcdir}/1.3.2-cygwin-types.patch
  patch -p2 -i ${srcdir}/1.3.3-cygwin-bindresvport.patch
  patch -p2 -i ${srcdir}/1.3.3-includedir.patch
  patch -p2 -i ${srcdir}/1.3.4-no-errqueue.patch

  autoreconf -fiv
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  cd libtirpc-${pkgver}

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --build=${CYGWIN_CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gssapi

  make
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package_libtirpc() {
  depends=('gcc-libs')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/etc ${pkgdir}/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libtirpc-devel() {
  pkgdesc="Libtirpc headers and libraries"
  groups=('development')
  depends=("libtirpc=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
