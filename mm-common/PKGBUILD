# Maintainer: Jeremy Drake <github@jdrake.com>

pkgname=mm-common
pkgver=1.0.7
pkgrel=1
pkgdesc="Common build files of the C++ bindings"
url="https://gitlab.gnome.org/GNOME/mm-common"
arch=('i686' 'x86_64')
makedepends=('meson' 'gcc')
license=('GPL2')
source=("https://download.gnome.org/sources/mm-common/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        https://gcc.gnu.org/onlinedocs/libstdc++/latest-doxygen/libstdc++.tag)
sha256sums=('494abfce781418259b1e9d8888c73af4de4b6f3be36cc75d9baa8baa0f2a7a39'
            'cb01f8aebdb7b9874b100d13a006fb688ee25d2e93e02ff294e6c2483606c7a3')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp ../libstdc++.tag doctags
}

build() {
  mkdir "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"
  meson \
    --buildtype=plain \
    --prefix=/usr \
    "../${pkgname}-${pkgver}"

  meson compile
}

check() {
  cd "${srcdir}/build-${CARCH}"
  meson test --print-errorlogs
}

package() {
  cd "${srcdir}/build-${CARCH}"
  DESTDIR="${pkgdir}" meson install
}
